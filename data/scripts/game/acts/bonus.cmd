IF NOT DEFINED VERCODE EXIT
SETLOCAL ENABLEDELAYEDEXPANSION
SET TMP.WEAPON=!WEAPONS.REG_NAME.%1!
ENDLOCAL&SET TMP.WEAPON=%TMP.WEAPON%
SET EFFECT.BONUS_ATK=0
SET EFFECT.BONUS_CRIT=0
CALL :%TMP.WEAPON% || EXIT /B 0
EXIT /B 0

:Branch
EXIT /B 0

:Dustblade
EXIT /B 0

:Cold_Twill
CALL "%SCRIPTS_GAME%\acts\effect.cmd" SLOWDOWN %INPUTATK% 25
EXIT /B 0

:Comrade_Hammer
SET /A "TMP.CHANCE=%random% %% 100"
IF %TMP.CHANCE% LEQ 50 (
	SET /A "EFFECT.BONUS_ATK=(EQUIP.BONUS_ATK *350)/100"
	CALL "%SCRIPTS_GAME%\logger.cmd" ADD The [4mcomrades[0m[1m salute, blessing you with %RGB.ORANGE%%%EFFECT.BONUS_ATK%%[0m[1m extra damage!%RGB.YELLOW%
)
EXIT /B 0
:Stylefi
IF %SKILL.CRIT_RATE% LSS 19 (
	SET /A EFFECT.BONUS_CRIT+=10
) ELSE IF %SKILL.CRIT_RATE% EQU 19 (
	SET /A EFFECT.BONUS_CRIT+=5
)
EXIT /B 0

:Flora_Thrower
CALL "%SCRIPTS_GAME%\acts\effect.cmd" POISON-CREATE %INPUTATK% 4 26
EXIT /B 0

:Trident_of_Gawra
SET /A "TMP.CHANCE=%random% %% 100"
IF %TMP.CHANCE% LEQ 40 (
	CALL "%SCRIPTS_GAME%\acts\effect.cmd" SLOWDOWN %INPUTATK% 100
)
EXIT /B 0

:Infernal_Blade
SET /A "TMP.CHANCE=%random% %% 100"
IF %TMP.CHANCE% LEQ 80 CALL "%SCRIPTS_GAME%\acts\effect.cmd" FIRE-CREATE %INPUTATK% 7 20
EXIT /B 0

:Ornate_Cobalt
:: Calculate the damage to deal to the enemies
SET /A TMP.DMG=(((50 * SKILL.ATK) + EQUIP.BONUS_ATK + EFFECT.BONUS_ATK) *20) / 100
FOR /L %%I IN (1,1,%EN.MAX%) DO (
	IF NOT %%I==%INPUTATK% (
		:: Decrease the HP of the enemy.
		SET /A "ENEMY.HP.NOW.%%I-=%TMP.DMG%"
		
		:: Make sure that the enemy doesn't have less than 0 HP
		SETLOCAL ENABLEDELAYEDEXPANSION
		IF !ENEMY.HP.NOW.%%I! LEQ 0 (
			ENDLOCAL
			SET ENEMY.HP.NOW.%%I=0
		) ELSE ENDLOCAL
	)
)
EXIT /B 0

:Brainleader
FOR /L %%I IN (1,1,%EN.MAX%) DO (
	:: Decrease the HP of the enemy
	SET /A ENEMY.HP.NOW.%%I-=9750
	
	:: Make sure that the enemy doesn't have less than 0 HP
	SETLOCAL ENABLEDELAYEDEXPANSION
	IF !ENEMY.HP.NOW.%%I! LEQ 0 (
		ENDLOCAL
		SET ENEMY.HP.NOW.%%I=0
	) ELSE ENDLOCAL
)

:: Decrease the HP of the player
IF %PLAYER.HP.NOW% GEQ 100 (
	SET /A PLAYER.HP.NOW=PLAYER.HP.NOW/100
) ELSE ( SET /A PLAYER.HP.NOW=1 )

CALL "%SCRIPTS_GAME%\logger.cmd" ADD %RGB.RED%UPON MY FOES, ENGULFING THEM IN DESTRUCTION!%RGB.YELLOW%%RGB.YELLOW%
CALL "%SCRIPTS_GAME%\logger.cmd" ADD %RGB.RED%TEMPEST, BRINGING DOWN THE WRATH OF THE HEAVENS%RGB.YELLOW%%RGB.YELLOW%
CALL "%SCRIPTS_GAME%\logger.cmd" ADD %RGB.RED%WITH THE FURY OF STORMS, I UNLEASH THE COSMIC%RGB.YELLOW%%RGB.YELLOW%
EXIT /B 0