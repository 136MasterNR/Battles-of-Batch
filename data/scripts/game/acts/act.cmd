IF NOT DEFINED VERCODE EXIT
CALL :%1 %2 %3 && (
	CALL "%SCRIPTS_GAME%\turn.cmd"
	CALL "%SCRIPTS_GAME%\turn.cmd" AV-SINGLE %CURR_TURN%
	CALL "%SCRIPTS_GAME%\turn.cmd" AV-CORRECT
)

EXIT /B 0





:ATTACK
SET PLAYER.ATTACK.AMOUNT=
SET PLAYER.ATTACK.ENEMY.R=
SET ATK.CRIT=
SET CRIT.CHANCE=
SET ENEMY.IS.HP=

:: Add weapon bonus effects
CALL "%SCRIPTS_GAME%\acts\bonus.cmd" %WIELDING.WEAPON%

SET /A CRIT.RATE=(SKILL.CRIT_RATE*SKILL.CRIT_RATE.BASE)+EFFECT.BONUS_CRIT
SET /A CRIT.CHANCE=%random% %% 100
SET /A PLAYER.ATTACK.CNT1=((SKILL.ATK.BASE * SKILL.ATK) + EQUIP.BONUS_ATK + EFFECT.BONUS_ATK) / 2
SET /A PLAYER.ATTACK.AMOUNT=%random% %% PLAYER.ATTACK.CNT1 +PLAYER.ATTACK.CNT1

:: Sound Effects
IF "%AUDIO.VALUE%"=="TRUE" IF NOT %SFX.VOLUME% EQU 0 (
	SETLOCAL ENABLEDELAYEDEXPANSION
	SET /A SFX.RANDOM.CRIT=%random% %% 3+1
	SET /A SFX.RANDOM.ATK=%random% %% 5+1
	IF %CRIT.CHANCE% LEQ %CRIT.RATE% (
		CALL "%AUDIOMANAGER%" START game\player\attack\crit_%%SFX.RANDOM.CRIT%%.mp3 sfx False %SFX.VOLUME%
	) ELSE (
		SET "TARGETAUDIO=%SFX.ATK%!SFX.RANDOM.ATK!.mp3"
		CALL "%AUDIOMANAGER%" START game\player\attack\swing_%%SFX.RANDOM.ATK%%.mp3 sfx False %SFX.VOLUME%
	)
	ENDLOCAL
)

IF %CRIT.CHANCE% LEQ %CRIT.RATE% (
	SET /A ATK.CRIT=PLAYER.ATTACK.AMOUNT*3
	SET /A ATK.CRIT=ATK.CRIT/4
	SET /A PLAYER.ATTACK.AMOUNT+=ATK.CRIT
) ELSE (
	SET ATK.CRIT=0
)

IF DEFINED INPUTATK ( SET "PLAYER.ATTACK.ENEMY.R=%INPUTATK%" ) ELSE SET /A "PLAYER.ATTACK.ENEMY.R=%random% %% %EN.MAX% +1"
SET PLAYER.ATTACK.ENEMY=%PLAYER.ATTACK.ENEMY.R%
SET /A ENEMY.HP.NOW.%PLAYER.ATTACK.ENEMY.R%=ENEMY.HP.NOW.%PLAYER.ATTACK.ENEMY% -%PLAYER.ATTACK.AMOUNT%
SET /A ENEMY.IS.HP=ENEMY.HP.NOW.%PLAYER.ATTACK.ENEMY%
IF %ENEMY.IS.HP% LSS 0 ( SET ENEMY.HP.NOW.%PLAYER.ATTACK.ENEMY%=0)
SET PLAYER.ATTACK.AMOUNT.%PLAYER.ATTACK.ENEMY.R%=%PLAYER.ATTACK.AMOUNT%

:: Log the action
SETLOCAL ENABLEDELAYEDEXPANSION
SET TMP.ENEMY=!ENEMY.TYPE.%INPUTATK%!
SET TMP.WEAPON=!WEAPONS.REG_NAME.%WIELDING.WEAPON%!
IF NOT DEFINED TMP.WEAPON SET TMP.WEAPON=Fists
IF %CRIT.CHANCE% LEQ %CRIT.RATE% SET TMP.CRIT_MSG=^(+CRIT!^)
ENDLOCAL&CALL "%SCRIPTS_GAME%\logger.cmd" ADD You striked [4m%TMP.ENEMY%[24m ^(#%INPUTATK%^) using %RGB.YELLOW%%TMP.WEAPON:_= %[0m[1m! %RGB.ORANGE%%TMP.CRIT_MSG%
EXIT /B 0





:Bomb
IF NOT DEFINED ITEM.REG_LVL.BOMB (
	ECHO.[47;3H%RGB%255;18;18mYou don't have any bombs![0m
	PAUSE>NUL                        
	EXIT /B 1
)

:: Play SFX
IF "%AUDIO.VALUE%"=="TRUE" IF NOT %SFX.VOLUME% EQU 0 START "" /MIN CMD /C "%AUDIOMANAGER%" START game\player\substat\bomb.mp3 sfx False %SFX.VOLUME%

IF %ITEM.REG_LVL.BOMB% LEQ 1 (
	(
		TYPE "%PLAYERDATA.ITEMS%" | FINDSTR /V Bomb
	)>"%PLAYERDATA.ITEMS%.new"
	MOVE "%PLAYERDATA.ITEMS%.new" "%PLAYERDATA.ITEMS%" >NUL
	CALL "%ITEMS.LOADER%" REMOVE-ITEM-EQ Bomb
	SET ITEM.REG_LVL.BOMB=
) ELSE CALL "%ITEMS.LOADER%" REPLACE-MAT Bomb 1-2 ITEMS
ENDLOCAL
CALL "%ITEMS.LOADER%" LIST
CALL "%ITEMS.LOADER%" LIST_EQ
IF ERRORLEVEL 60 ECHO.ERROR&PAUSE>NUL&EXIT /B 1
SET PLAYER.ATTACK.AMOUNT=0
FOR /L %%I IN (1,1,%EN.MAX%) DO (
	CALL :Bomb-CALC %%I
)

CALL "%SCRIPTS_GAME%\turn.cmd" AV-INCREASE-ALL 100

SET PLAYER.ATTACK.ENEMY=all enemies

CALL "%SCRIPTS_GAME%\logger.cmd" ADD You striked [4mall enemies[24m using %RGB.YELLOW%Bomb[0m[1m!%RGB.YELLOW%
EXIT /B 0

:Bomb-CALC
SET /A TMP=ENEMY.HP.NOW.%1
IF %TMP%==0 (SET "PLAYER.ATTACK.AMOUNT.%1=0") ELSE SET /A "PLAYER.ATTACK.AMOUNT.%1=%random% %% 3 +9"
IF %INPUTATK% EQU %1 SET /A "PLAYER.ATTACK.AMOUNT.%1=PLAYER.ATTACK.AMOUNT.%1 * 3"
IF NOT %TMP% LEQ 0 (
	SET /A TMP=TMP - PLAYER.ATTACK.AMOUNT.%1
)
SET /A PLAYER.ATTACK.AMOUNT+=PLAYER.ATTACK.AMOUNT.%1
IF %TMP% LSS 0 ( SET /A "TMP=0" ) ELSE IF %TMP% LSS 0 SET "PLAYER.ATTACK.AMOUNT.%1=0"

SET ENEMY.HP.NOW.%1=%TMP%
EXIT /B 0





:Heal
IF NOT DEFINED ITEM.REG_LVL.HEAL (
	ECHO.[47;3H%RGB%255;18;18mYou don't have enough items![0m
	PAUSE>NUL
	ECHO.[47;3H                            
	EXIT /B 1
)
IF %ITEM.REG_LVL.HEAL% LEQ 1 (
	CALL "%ITEMS.LOADER%" REMOVE-ITEM-EQ Heal
	(
		TYPE "%PLAYERDATA.ITEMS%" | FINDSTR /V Heal
	)>"%PLAYERDATA.ITEMS%.new"
	MOVE "%PLAYERDATA.ITEMS%.new" "%PLAYERDATA.ITEMS%" >NUL
	SET ITEM.REG_LVL.HEAL=
) ELSE CALL "%ITEMS.LOADER%" REPLACE-MAT Heal 1-2 ITEMS
CALL "%ITEMS.LOADER%" LIST
CALL "%ITEMS.LOADER%" LIST_EQ
SET /A "PLAYER.HEAL.AMOUNT=%random% %% (PLAYER.HP.FULL - ((PLAYER.HP.FULL * 3) / 5))/2 +(PLAYER.HP.FULL - ((PLAYER.HP.FULL * 2) / 3))"
SET /A TMP.P_HP=PLAYER.HP.FULL-PLAYER.HP.NOW
IF NOT %PLAYER.HP.NOW% GEQ %PLAYER.HP.FULL% SET /A "PLAYER.HP.NOW=%PLAYER.HP.NOW% +%PLAYER.HEAL.AMOUNT%"
IF %PLAYER.HP.NOW% GTR %PLAYER.HP.FULL% (
	SET /A "PLAYER.HP.NOW=%PLAYER.HP.FULL%"
	SET PLAYER.HEAL.AMOUNT=%TMP.P_HP%
)

CALL "%SCRIPTS_GAME%\acts\effect.cmd" BLEED-STOP-ALL

CALL "%SCRIPTS_GAME%\logger.cmd" ADD You %RGB.YELLOW%healed[0m[1m yourself by %RGB.GREEN%%PLAYER.HEAL.AMOUNT%[0m[1m points![0m
EXIT /B 0





:NOTHING
CALL "%SCRIPTS_GAME%\logger.cmd" ADD You did [4mabsolutely[24m %RGB.YELLOW%nothing[0m!%RGB.YELLOW%[0m
EXIT /B 0
